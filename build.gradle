/*

ForgeGradle building for libsandstone! This script is meant to be a easy way to build the mod, as well as example for implementing ForgeGradle in your own mods.

How to use:

1. Open up Terminal (Unix) or cmd (Windows).
2. Navigate to the mod's main folder.
3. './gradlew setupCIWorkspace'
4. './gradlew build'
5. Enjoy!

*/

// This simple grabs the latest 'forge' plugin, or ForgeGradle, from Forge's servers.
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
  maven {
    name = "trainerguy22"
    url = "http://trainerguy22.github.io/builds/maven/"
  }
}

// We apply the 'forge' plugin, so ForgeGradle can do it's stuff.
apply plugin: 'forge'

group = 'lib.enderwizards.sandstone' // Your main package.
version = "1.0.0"
archivesBaseName = 'LibSandstone'

minecraft {
	version = "1.7.10-10.13.0.1180"
}

dependencies {
	compile 'trainerguy22:jtoml:1.0.1'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace stuff in mcmod.info and in .lang files, nothing else.
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // Replace version and mcversion.
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // Copy everything else that wasn't replaced.
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}
